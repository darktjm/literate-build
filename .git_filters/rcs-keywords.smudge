#!/usr/bin/perl
#
# @brief  Git filter to implement rcs keyword expansion as seen in cvs and svn.
# @author Martin Turon
#
# Usage:
#    .git_filter/rcs-keywords.smudge file_path < file_contents
# 
# To add keyword expansion:
#    <project>/.gitattributes                    - *.c filter=rcs-keywords
#    <project>/.git_filters/rcs-keywords.smudge  - copy this file to project
#    <project>/.git_filters/rcs-keywords.clean   - copy companion to project
#    ~/.gitconfig                                - add [filter] lines below
#
# [filter "rcs-keywords"]
#	clean  = .git_filters/rcs-keywords.clean
#	smudge = .git_filters/rcs-keywords.smudge %f
#
# Copyright (c) 2009-2011 Turon Technologies, Inc.  All rights reserved.

$path = shift;
$path =~ /.*\/(.*)/;
$filename = $1;

use DateTime::Format::Strptime;

if (0 == length($filename)) {
	$filename = $path;
}

# Need to grab filename and to use git log for this to be accurate.
# could add --grep=git-svn-id, but shouldn't be necessary
# -n 1 replaces | head more efficiently and includes entire log message
$rev = `git log -n 1 --date=iso -- $path`;
$rev =~ /^Author:\s*(.*?)\s*$/m;
$author = $1;
$author =~ /\s*(.*?)\s*<.*/;
$name = $1;
$rev =~ /^Date:\s*(.*?)\s*$/m;
$date = $1;
$rev =~ /^commit (.*)$/m;
$ident = $1;
$rev =~ /^    git-svn-id.*@([0-9]+)/m;
$svnrev = $1 || $ident;
$strp = DateTime::Format::Strptime->new(pattern => '%F %T %z');
$ldate = $strp->parse_datetime($date)->strftime('(%c)');
 

while (<STDIN>) {
    s/\$Date[^\$]*\$/\$Date: $date $ldate \$/;
    s/\$Author[^\$]*\$/\$Author: $author \$/;
    s/\$Id[^\$]*\$/\$Id: $filename $svnrev $date $name \$/;
    s/\$File[^\$]*\$/\$File: $filename \$/;
    s/\$Source[^\$]*\$/\$Source: $path \$/;
	s/\$Revision[^\$]*\$/\$Revision: $svnrev \$/;
} continue {
    print or die "-p destination: $!\n";
}
